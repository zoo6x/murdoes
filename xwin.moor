vocabulary xwin
xwin definitions

vocabulary internal
internal definitions
								\\ Syscalls
								\ https://syscalls.mebeim.net/?table=x86/64/x64/latest
: read		0 syscall3 ;
: write		1 syscall3 ;
: poll		7 syscall3 ;
: socket	29 syscall3 ;
: connect	2a syscall3 ;
: fcntl		48 syscall3 ;

:: AF_UNIX		 1 val
:: SOCK_STREAM		 1 val
:: F_GETFL 		 3 val
:: F_SETFL		 4 val
:: O_NONBLOCK          800 val

: int           4 field ;
: short         2 field ;
struct pollfd internal
    int		fd
    short	events
    short	revents

    :: POLLIN	 1 val    
    :: POLLERR   8 val
    :: POLLHUP  10 val
;struct

								\\ X Protocol Definitions
: uint8_t	1 field ;
: uint16_t	2 field ;
: int16_t	2 field ;
: uint32_t	4 field ;
: xcb_keycode_t	uint8_t ;
: xcb_gcontext_t uint32_t ;
: xcb_drawable_t uint32_t ;
: xcb_window_t uint32_t ;
: xcb_visualid_t uint32_t ;
: xcb_timestamp_t uint32_t ;

struct xcb_setup_request internal
    uint8_t	byte_order
    uint8_t	pad0
    uint16_t	protocol_major_version
    uint16_t	protocol_minor_version
    uint16_t	authorization_protocol_name_len
    uint16_t	authorization_protocol_data_len
    uint8_t	pad1_1
    uint8_t	pad1_2
;struct

struct xcb_setup_response internal
    uint8_t	status
    uint8_t	pad0
    uint16_t	protocol_major_version
    uint16_t	protocol_minor_version
    uint16_t	length
    uint32_t	release_number
    uint32_t	resource_id_base
    uint32_t	resource_id_mask
    uint32_t	motion_buffer_size
    uint16_t	vendor_len
    uint16_t	maximum_request_length
    uint8_t	roots_len
    uint8_t	pixmap_formats_len
    uint8_t	image_byte_order
    uint8_t	bitmap_format_bit_order
    uint8_t	bitmap_format_scanline_unit
    uint8_t	bitmap_format_scanline_pad
    xcb_keycode_t	min_keycode
    xcb_keycode_t	max_keycode
    uint8_t	pad1_1
    uint8_t	pad1_2
    uint8_t	pad1_3
    uint8_t	pad1_4
;struct

struct xcb_create_gc_request internal
    uint8_t        major_opcode
    uint8_t        pad0
    uint16_t       length
    xcb_gcontext_t cid
    xcb_drawable_t drawable
    uint32_t       value_mask
    uint32_t       value1
    uint32_t       value2

    :: XCB_CREATE_GC 37 val
    :: BG 4 val
    :: FG 8 val
    :: FONT 4000 val
;struct

struct xcb_create_window_request internal
    uint8_t        major_opcode
    uint8_t        depth
    uint16_t       length
    xcb_window_t   wid
    xcb_window_t   parent
    int16_t        x
    int16_t        y
    uint16_t       width
    uint16_t       height
    uint16_t       border_width
    uint16_t       _class
    xcb_visualid_t visual
    uint32_t       value_mask
    uint32_t       value1
    uint32_t       value2

    :: XCB_CREATE_WINDOW 1 val
    
    :: FLAG_WIN_BG_COLOR 2 val
    :: FLAG_WIN_EVENT  800 val

    :: EVENT_KEY_PRESS         1 val
    :: EVENT_KEY_RELEASE       2 val
    :: EVENT_BUTTON_PRESS      4 val
    :: EVENT_BUTTON_RELEASE    8 val
    :: EVENT_POINTER_MOTION   40 val
    :: EVENT_EXPOSURE       8000 val
    :: EVENT_RESIZE        40000 val
;struct

struct xcb_map_window_request internal
    uint8_t      major_opcode
    uint8_t      pad0
    uint16_t     length
    xcb_window_t window

    :: XCB_MAP_WINDOW 8 val
;struct    

struct xcb_put_image_request internal
    uint8_t        major_opcode
    uint8_t        format
    uint16_t       length
    xcb_drawable_t drawable
    xcb_gcontext_t gc
    uint16_t       width
    uint16_t       height
    int16_t        dst_x
    int16_t        dst_y
    uint8_t        left_pad
    uint8_t        depth
    uint8_t        pad0
    uint8_t        pad1

    :: XCB_PUT_IMAGE 48 val
;struct

struct xcb_poly_line_request internal
    uint8_t        major_opcode
    uint8_t        coordinate_mode
    uint16_t       length
    xcb_drawable_t drawable
    xcb_gcontext_t gc
    uint16_t	   x0
    uint16_t	   y0
    uint16_t	   x1
    uint16_t	   y1

    :: XCB_POLY_LINE 41 val
;struct

struct xcb_event internal
    uint8_t         response_type
    xcb_keycode_t   detail
    uint16_t        sequence
    xcb_timestamp_t time
    xcb_window_t    root
    xcb_window_t    event
    xcb_window_t    child
    int16_t         root_x
    int16_t         root_y
    int16_t         event_x
    int16_t         event_y
    uint16_t        state
    uint8_t         same_screen
    uint8_t         pad0
;struct

struct xcb_expose_event internal
    uint8_t      response_type
    uint8_t      pad0
    uint16_t     sequence
    xcb_window_t window
    uint16_t     x
    uint16_t     y
    uint16_t     width
    uint16_t     height
    uint16_t     count
    uint8_t      pad1
    uint8_t      pad2
;struct

:: KeyPress	2 val
:: KeyRelease	3 val
:: ButtonPress	4 val
:: ButtonRelease 5 val
:: Expose	c val
								\\ Variables and Buffers


:: xsocket	  -1 var 
:: id_base	   0 var
:: id_mask	   0 var
:: window_root_id -1 var
:: root_visual_id -1 var
:: id		   0 var
:: gc_id	   0 var
:: drawable_id     0 var
:: window_id       0 var

:: event	   0 var
:: detail          0 var
:: event_x	   0 var
:: event_y	   0 var

:: WIDTH	 780 val	\ 1920
:: HEIGHT	 438 val	\ 1080

:: #sockaddr_un 6c val
:: xsockaddr adr 1 2, #sockaddr_un here ,0" /tmp/.X11-unix/X0" here >< - - allot

xcb_setup_request struct: xsetup
xsetup
char l	byte_order .!
0b	protocol_major_version .!
_

xcb_setup_response struct: xsetup_response adr 4000 xcb_setup_response #struct - allot
xcb_create_gc_request struct: xcreategc_request
xcb_create_window_request struct: xcreatewindow_request
xcb_map_window_request struct: xmapwindow_request
pollfd struct: xpoll
xcb_event struct: xevent

xcb_put_image_request struct: xputimage_request here 10000 allot | 8000 55 fill 8000 + 8000 99 fill
xcb_poly_line_request struct: xpolyline_request

xwin definitions internal

: XNextId
        id @ | id_mask @ and id_base @ or >< 1+ id ! ;
: XSocket
	0 SOCK_STREAM AF_UNIX socket				?? | 0 < ?? if abort" Failed to create socket" then
	xsocket ! ;
: XConnect
        #sockaddr_un xsockaddr xsocket @ connect 		?? | 0 < ?? if abort" Failed to connect" then _ ;
: XWrite xsocket @ write ;
: XRead  xsocket @ read ;
: XNonBlock
	yellow cr ." non-blocking socket " norm
	0 F_GETFL xsocket @ fcntl				?? | 0 <    if abort" Failed to fcntl F_GET" then
	O_NONBLOCK or F_SETFL xsocket @ fcntl			?? | 0 <    if abort" Failed to fcntl F_SET" then _ ; 
: XPollInit
	xpoll xsocket @ [ pollfd ] fd .! POLLIN events .! _
	[ internal ]
;
: XPoll -1 1 xpoll poll 					| 0 <=   if abort" Failed to poll" then _ 
        xpoll [ pollfd ] revents .@ _				| POLLERR = if abort" POLLERR" then
							          POLLHUP = if abort" POLLHUP" then
	[ internal ]
	20 xevent XRead						|  0 <   if abort" Failed to read event reply" then
	0 = if
		0 0
	else 
		xevent
		[ xcb_event ] response_type .@ >< [ internal ] | event !
		Expose = if
			[ internal ] 0 detail !
			[ xcb_expose_event ] width .@ >< [ internal ] event_x !
			[ xcb_expose_event ] height .@ >< [ internal ] event_y !
		else
			[ xcb_event ] detail .@ >< [ internal ] detail !
			[ xcb_event ] event_x .@ >< [ internal ] event_x !
			[ xcb_event ] event_y .@ >< [ internal ] event_y !
		then
		_
		event @ 1
	then
;
: XSetup
	xcb_setup_request #struct xsetup XWrite			?? | 0 < ?? if abort" Failed to write setup request" then _
	purple cr ." Reading setup response..." norm
	4000 xsetup_response XRead	 			?? | 0 < ?? if abort" Failed to read setup response" then _
	xsetup_response [ xcb_setup_response ] status .@ ><	??   0 =    if abort" Setup response status 'Failed'" then

	maximum_request_length .@ cr red ." Max request length = " >< ?? _ cr
	[ internal xcb_setup_response ] resource_id_base .@ >< ?? [ internal ] id_base !
	[ internal xcb_setup_response ] resource_id_mask .@ >< ?? [ internal ] id_mask !
	[ internal xcb_setup_response ] vendor_len .@ >< ?? >< pixmap_formats_len .@ >< ?? 3 shl ><
	[ internal ] xcb_setup_response #struct + + + | ?? 4@ ?? window_root_id ! 20 + 4@ ?? root_visual_id !
;
: XCreateGC
	xcreategc_request [ xcb_create_gc_request ]		cr yellow ." xcb_create_gc_request " norm
	XCB_CREATE_GC ?? major_opcode .! #struct ?? 2/ 2/ ?? length .!
	>< ?? cid .! >< ?? drawable .! BG FG or ?? value_mask .! 00ffff value1 .! ff0000 value2 .!
	#struct >< XWrite					?? #struct <> if abort" Failed to write create GC request" then
	[ internal ]
;
: XCreateWindow
	xcreatewindow_request [ xcb_create_window_request ]	cr yellow ." xcb_create_window_request " norm
	XCB_CREATE_WINDOW major_opcode .! 0 depth .! #struct 2/ 2/ length .!
	>< wid .! 
	[ internal ] window_root_id @ [ xcb_create_window_request ] parent .!
	100 x .! 100 y .! 200 width .! 200 height .! 1 border_width .! 1 _class .!
	[ internal ] root_visual_id @ [ xcb_create_window_request ] visual .!
	FLAG_WIN_BG_COLOR FLAG_WIN_EVENT or value_mask .! ff7f00 value1 .!
	EVENT_KEY_PRESS EVENT_KEY_RELEASE or EVENT_EXPOSURE or EVENT_BUTTON_PRESS or EVENT_BUTTON_RELEASE or EVENT_POINTER_MOTION or
	EVENT_RESIZE or
	value2 .!
	#struct >< XWrite					?? #struct <> if abort" Failed to write create window request" then
	[ internal ]
;
: XMapWindow
	xmapwindow_request [ xcb_map_window_request ]
	XCB_MAP_WINDOW major_opcode .! #struct 2/ 2/ length .!
	[ internal ] window_id @ [ xcb_map_window_request ] window .!
	#struct >< XWrite					?? #struct <> if abort" Failed to write map window request" then
	[ internal ]
;
: XDrawImage
	xputimage_request [ xcb_put_image_request ]		cr yellow ." xcb_put_image_request " norm
	XCB_PUT_IMAGE major_opcode .! 2 format .! #struct 2/ 2/ 4000 + ?? length .!
	[ internal ] drawable_id @ ?? [ xcb_put_image_request ] drawable .!
	[ internal ] gc_id @ ?? [ xcb_put_image_request ] gc .!
	[ internal ] 200 [ xcb_put_image_request ] width .!
	[ internal ] 20 [ xcb_put_image_request ] height .!
	0 dst_x .! 100 dst_y .! 0 left_pad .! 18 depth .!
	cr green | 100 dump cr
	\ #struct 10000 + ?? >< XWrite				 ?? #struct 10000 + <> if abort" Failed to write put image request" then
	| #struct >< .S XWrite					 ?? #struct <> if abort" Failed to write put image request header" then
	#struct + 10000 .S >< XWrite 				 ?? 10000   <> if abort" Failed to write put image request body" then
	[ internal ]
;
: XPolyLine
	xputimage_request [ xcb_poly_line_request ]		cr yellow ." xcb_poly_line_request " norm
	XCB_POLY_LINE major_opcode .! 0 coordinate_mode .! #struct 2/ 2/ length .!
	[ internal ] drawable_id @ ?? [ xcb_poly_line_request ] drawable .!
	[ internal ] gc_id @ ?? [ xcb_poly_line_request ] gc .!
	50 x0 .! 50 y0 .! 250 x1 .! 250 y1 .!
	#struct >< XWrite				 	?? #struct <> if abort" Failed to write poly line request" then
	[ internal ]
;
: XPollEvents
	\ XNonBlock
	XPollInit
	yellow cr ." Polling events... " norm cr
	begin 
		XPoll 
	while
		_
		cr purple event @ . white detail @ . tab green event_x @ . bs yellow ." , " green event_y @ . norm .S
		event @
		| KeyPress = if detail @ 9 = if exit then then	\ ESC
		| Expose   = if XDrawImage XPolyLine then
		| 19       = if then
		_

	repeat
;
: XRun
        XSocket XConnect XSetup
	XNextId | gc_id ! XNextId drawable_id ! window_root_id @ ><
	XCreateGC
	drawable_id @ | window_id !
	XCreateWindow XMapWindow
	XPollEvents
; 	

XRun

